// Gradle plugins
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'sonatype-nexus-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven {
            name = 'forge-repo'
            url = 'http://files.minecraftforge.net/maven/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "java"
    id "maven"
    id "signing"
}

apply plugin: "forge"

// Project repositories
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'oss-sonatype'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
}

// Project dependencies
dependencies {
    compile 'com.almuradev:almurasdk:1.7.10-1277:dev'
    compile 'ninja.leaping.configurate:configurate-yaml:0.2'
}

// Gradle default tasks
defaultTasks 'licenseFormat', 'build'

// Basic project information
group = 'com.almuradev'
archivesBaseName = 'guide'

// Version information
ext.minecraftVersion = '1.7.10'
ext.forgeVersion = '10.13.2.1277'
version = minecraftVersion.substring(0, 3) + '-' + forgeVersion.substring(forgeVersion.size() - 4)

// Extended project information
ext.projectName = 'Guide'
ext.inceptionYear = '2015'
ext.packaging = 'jar'
ext.url = 'http://github.com/AlmuraDev'
ext.description = 'Le Foo, Le Bar'
ext.organization = 'AlmuraDev'

// Java version target/property compatibility
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// MinecraftForge version
minecraft {
    version = project.ext.minecraftVersion + '-' + project.ext.forgeVersion
    runDir = "run/"
}

// Configuration settings
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds' // Always check for new snapshots
}

// Filter, process, and include resources
processResources {
    // Include in final JAR
    from 'LICENSE.txt'

    // Replace variables
    from('src/main/resources/mcmod.info') {
        expand 'modid': projectName.toLowerCase(),
               'name': projectName,
               'description': project.ext.description,
               'version': version,
               'mcversion': minecraft.version,
               'url': url,
               'authorList': organization
    }
}

// Source compiler configuration
tasks.withType(JavaCompile) {
    options.compilerArgs += [ '-Xlint:all', '-Xlint:-path', '-Xlint:-processing' ]
    options.deprecation = true
    options.encoding = 'utf8'
}

// License header formatting
license {
    ext.name = project.name
    ext.organization = organization
    ext.url = url
    ext.year = inceptionYear
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    includes(["**/*.java"])
}

// JAR manifest configuration
jar.manifest.mainAttributes(
        "Built-By": System.properties['user.name'],
        "Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title": name,
        "Implementation-Version": version,
        "Implementation-Vendor": url,
)
