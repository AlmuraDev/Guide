buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge-repo'
            url = 'http://files.minecraftforge.net/maven/'
        }
        maven {
            name = 'sonatype-nexus'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

defaultTasks 'licenseFormat', 'build'

apply plugin: 'forge'
apply plugin: 'license'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.almuradev'
archivesBaseName = 'guide'

ext.name = 'Guide'
ext.inceptionYear = '2015'
ext.packaging = 'jar'
ext.url = 'http://github.com/AlmuraDev/Guide'
ext.organization = 'AlmuraDev'
ext.minecraftVersion = '1.7.10'
ext.forgeVersion = '10.13.4.1448'
ext.guideVersion = '1'
version = minecraftVersion + '-' + forgeVersion.substring(forgeVersion.size() - 4) + '-' + ext.guideVersion

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'sonatype-nexus'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
    maven {
        name = 'sponge-public'
        url = 'http://repo.spongepowered.org/maven/'
    }
}

minecraft {
    version = project.ext.minecraftVersion + '-' + project.ext.forgeVersion + '-' + project.ext.minecraftVersion
    mappings = "stable_12"
    runDir = "run/"
}

dependencies {
    compile('com.almuradev:almurasdk:1.7.10-1448-1-SNAPSHOT:dev') {
        transitive = true
    }
}

processResources {
    inputs.property "version", version
    inputs.property "mcversion", project.minecraft.version

    from 'LICENSE.txt'
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    ext.year = project.inceptionYear
    sourceSets = project.sourceSets
    header file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    includes(["**/*.java"])
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:all',
            '-Xlint:-path',
            '-Xlint:-processing'
    ]
    options.deprecation = true
    options.encoding = 'utf8'
}

def manifestEntries = [
        "Built-By"              : System.properties['user.name'],
        "Created-By"            : System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        "Implementation-Title"  : name,
        "Implementation-Version": version,
        "Implementation-Vendor" : url
]

jar {
    manifest.mainAttributes(manifestEntries)
}

task sourceJar(type: Jar, dependsOn: sourceMainJava) {
    from "build/sources/java"
    from "build/resources/main/java"
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = "javadoc"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = "dev"
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives deobfJar
}

uploadArchives {
    repositories.mavenDeployer {
        if (version.endsWith('-SNAPSHOT')) {
            ext.sonatypeUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
        } else {
            ext.sonatypeUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'

            signing {
                required {gradle.taskGraph.hasTask("uploadArchives")}

                sign configurations.archives

                beforeDeployment {
                    org.gradle.api.artifacts.maven.MavenDeployment deployment -> signing.signPom(deployment)
                }
            }
        }

        repository(url: sonatypeUrl) {
            authentication(userName: System.getenv("sonatypeUsername"), password: System.getenv("sonatypePassword"))
        }

        pom.project {
            name name
            artifactId archivesBaseName
            packaging packaging
            inceptionYear inceptionYear
            url url
            description description

            scm {
                connection 'scm:git:git://github.com/AlmuraDev/Guide.git'
                developerConnection 'scm:git:ssh://git@github.com:AlmuraDev/Guide.git'
                url 'https://github.com/AlmuraDev/Guide'
            }

            licenses {
                license {
                    name 'The MIT License'
                    url 'http://www.tldrlegal.com/license/mit-license'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Zidane'
                    name 'Chris Sanders'
                    email 'zidane@outlook.com'
                }
                developer {
                    id 'Grinch'
                    name 'Steven Downer'
                    email 'grinch@outlook.com'
                }
                developer {
                    id 'Dockter'
                    name 'Mike Howe'
                    email 'dockter@almuramc.com'
                }
            }

            organization {
                name organization
                url url
            }

            issueManagement {
                system 'github'
                url 'https://github.com/AlmuraDev/Guide/issues'
            }
        }
    }
}
